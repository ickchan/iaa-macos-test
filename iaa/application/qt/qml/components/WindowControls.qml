import QtQuick
import QtQuick.Controls
import QtQuick.Window

// Windows 窗口控件（最小化 / 最大化 / 关闭）。
// hit-test 布局（从右到左，名 46px）：
//   close    → HTCLIENT（QML 处理点击 + hover）
//   maximize → HTMAXBUTTON（OS 处理贴靠布局弹窗；hover 由 maxHoverBridge 中转）
//   minimize → HTCLIENT（QML 处理点击 + hover）
Row {
    id: root

    signal minimizeRequested()
    signal closeRequested()

    // maxHoverBridge 从 QML 上下文全局属性获取，无需作为属性传入
    required property var window

    visible: Qt.platform.os === "windows"
    spacing: 0

    readonly property string _iconFont: "FluentSystemIcons-Regular"
    property bool _maxHoveredByOS: false

    Connections {
        target: (Qt.platform.os === "windows" && typeof maxHoverBridge !== "undefined")
            ? maxHoverBridge : null
        function onHoveredChanged(hovered) { root._maxHoveredByOS = hovered }
    }

    // ── Minimize（FluentSystemIcons subtract_20 = \uEBD0）──────────────────
    Rectangle {
        width: 46; height: root.height
        color: _minHover.containsMouse
            ? Qt.rgba(palette.windowText.r, palette.windowText.g, palette.windowText.b, 0.08)
            : "transparent"
        Text {
            anchors.centerIn: parent
            font.family: root._iconFont
            font.pixelSize: 16
            text: "\uEBD0"
            color: palette.windowText
        }
        MouseArea {
            id: _minHover
            anchors.fill: parent
            hoverEnabled: true
            onClicked: root.minimizeRequested()
        }
    }

    // ── Maximize / Restore（maximize_20 = \uE7EB / square_multiple_20 = ）──
    Rectangle {
        width: 46; height: root.height
        readonly property bool _hovered: Qt.platform.os === "windows"
            ? root._maxHoveredByOS
            : _maxHover.containsMouse
        color: _hovered
            ? Qt.rgba(palette.windowText.r, palette.windowText.g, palette.windowText.b, 0.08)
            : "transparent"
        Text {
            anchors.centerIn: parent
            font.family: root._iconFont
            font.pixelSize: 16
            text: root.window.visibility === Window.Maximized ? "\uEB96" : "\uE7EB"
            color: palette.windowText
        }
        MouseArea {
            id: _maxHover
            anchors.fill: parent
            hoverEnabled: Qt.platform.os !== "windows"
            enabled:      Qt.platform.os !== "windows"
            onClicked: {
                if (root.window.visibility === Window.Maximized) root.window.showNormal()
                else root.window.showMaximized()
            }
        }
    }

    // ── Close（FluentSystemIcons dismiss_20 = \uF369）─────────────────
    Rectangle {
        width: 46; height: root.height
        color: _closeHover.containsMouse ? "#c42b1c" : "transparent"
        Text {
            anchors.centerIn: parent
            font.family: root._iconFont
            font.pixelSize: 16
            text: "\uF369"
            color: _closeHover.containsMouse ? "white" : palette.windowText
        }
        MouseArea {
            id: _closeHover
            anchors.fill: parent
            hoverEnabled: true
            onClicked: root.closeRequested()
        }
    }
}
